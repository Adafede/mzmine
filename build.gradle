import org.gradle.internal.os.OperatingSystem

plugins {
    id "java"
    id "application"
    id "org.openjfx.javafxplugin" version "0.0.8"
    id "org.beryx.runtime" version "1.3.0"
}

group = "io.github.mzmine"
version = "2.50"
description = "MZmine"
sourceCompatibility = "12"
targetCompatibility = "12"
defaultTasks "runtimeZip"

// Check the OS
project.ext.osSuffix = OperatingSystem.current().getName().replaceAll(" ", "_")
if (OperatingSystem.current().isMacOsX())
    project.ext.osSuffix = "mac"
if (OperatingSystem.current().isLinux())
    project.ext.osSuffix = "linux"
if (OperatingSystem.current().isWindows())
    project.ext.osSuffix = "win"
println "OS detected as " + project.ext.osSuffix


repositories {
    mavenLocal()
    maven {
        url = "https://www.ebi.ac.uk/Tools/maven/repos/content/groups/ebi-repo/"
    }
    maven {
        url = "file://" + projectDir + "/src/main/lib"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url = "http://repo.maven.apache.org/maven2"
    }
    maven {
        url = "https://bio.informatik.uni-jena.de/repository/libs-release-oss/"
    }
    maven {
        url = "https://www.xypron.de/repository"
    }
}

ext {
    msdkVersion = "0.0.27"
    cdkVersion = "2.3"
    jmztabVersion = "3.0.8"
    swaggerVersion = "1.5.22"
    okhttpVersion = "2.7.5"
    guavaVersion = "28.0-jre"
    slf4jVersion = "1.7.28"
    poiVersion = "4.1.0"
    batikVersion = "1.11"
    javafxVersion = "12.0.2"
    controlsfxVersion = "11.0.0"
}

dependencies {
    compile "com.github.graphstream:gs-core:2.0-alpha"
    compile "com.github.graphstream:gs-ui-swing:2.0-alpha"
    compile "org.jfree:jfreechart:1.5.0"
    compile "org.jfree:jfreechart-fx:1.0.1"
    compile "org.jfree:jfreesvg:3.3"
    compile "com.orsonpdf:orsonpdf:1.9"
    compile "com.google.guava:guava:$guavaVersion"
    compile "org.apache.commons:commons-math:2.2"
    compile "edu.ucar:netcdf4:4.5.5"
    compile "org.ostermiller:utils:1.07.00"
    compile "com.google.code.findbugs:jsr305:3.0.2"
    compile "nz.ac.waikato.cms.weka:weka-stable:3.8.3"
    compile "net.sourceforge.jmol:jmol:14.29.27"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    compile "org.apache.poi:poi:$poiVersion"
    compile "org.apache.poi:poi-ooxml:$poiVersion"
    compile "org.openscience.cdk:cdk-core:$cdkVersion"
    compile "org.openscience.cdk:cdk-io:$cdkVersion"
    compile "org.openscience.cdk:cdk-standard:$cdkVersion"
    compile "org.openscience.cdk:cdk-silent:$cdkVersion"
    compile "org.openscience.cdk:cdk-formula:$cdkVersion"
    compile "org.openscience.cdk:cdk-sdg:$cdkVersion"
    compile "org.openscience.cdk:cdk-renderbasic:$cdkVersion"
    compile "org.openscience.cdk:cdk-renderawt:$cdkVersion"
    compile "org.openscience.cdk:cdk-data:$cdkVersion"
    compile "org.openscience.cdk:cdk-interfaces:$cdkVersion"
    compile "org.openscience.cdk:cdk-smiles:$cdkVersion"
    compile "uk.ac.ebi.jmzml:jmzml:1.7.11"
    compile "org.postgresql:postgresql:9.3-1102-jdbc41"
    compile "mysql:mysql-connector-java:5.1.34"
    compile "org.rsc.chemspider:chemspider-api:1.0.0"
    compile "org.nuiton.thirdparty:REngine:1.8-5"
    compile "org.nuiton.thirdparty:Rserve:1.8-5"
    compile "com.github.jbytecode:RCaller:3.0"
    compile "mzmine:jmprojection:1.0"
    compile "mzmine:gslibml:0.0.1"
    compile "uk.ac.ebi.pride:jmztab-modular-model:$jmztabVersion"
    compile "uk.ac.ebi.pride:jmztab-modular-util:$jmztabVersion"
    compile "org.apache.xmlgraphics:fop:1.0"
    compile "org.freehep:freehep-graphicsio-emf:2.4"
    compile "io.github.msdk:msdk-io-mzml:$msdkVersion"
    compile "io.github.msdk:msdk-io-netcdf:$msdkVersion"
    compile "io.github.msdk:msdk-featuredetection-adap3d:$msdkVersion"
    compile "io.github.msdk:msdk-id-sirius:$msdkVersion"
    compile "org.xerial:sqlite-jdbc:3.8.11.2"
    compile "org.du-lab.adap:adap:4.0.5"
    compile "gnf:clustering:20130919"
    compile "it.unimi.dsi:fastutil:8.2.2"
    compile "com.itextpdf:itextpdf:5.5.13.1"
    compile "org.apache.xmlgraphics:xmlgraphics-commons:2.3"
    compile "org.apache.xmlgraphics:batik-util:$batikVersion"
    compile "org.apache.xmlgraphics:batik-transcoder:$batikVersion"
    compile "org.apache.xmlgraphics:batik-codec:$batikVersion"
    compile "org.apache.xmlgraphics:batik-ext:$batikVersion"
    compile "org.apache.xmlgraphics:batik-awt-util:$batikVersion"
    compile "org.apache.xmlgraphics:batik-dom:$batikVersion"
    compile "org.apache.xmlgraphics:batik-svggen:$batikVersion"
    compile "org.apache.xmlgraphics:batik-svg-dom:$batikVersion"
    compile "org.apache.xmlgraphics:batik-anim:$batikVersion"
    compile "org.apache.xmlgraphics:batik-rasterizer:$batikVersion"
    compile "org.apache.xmlgraphics:batik-swing:$batikVersion"
    compile "org.apache.xmlgraphics:batik-gui-util:$batikVersion"
    compile "org.apache.xmlgraphics:batik-ttf2svg:$batikVersion"
    compile "org.apache.xmlgraphics:batik-parser:$batikVersion"
    compile "net.sf.epsgraphics:epsgraphics:1.2"
    compile "com.miglayout:miglayout:3.7.4"
    compile "javax.xml.bind:jaxb-api:2.3.1"
    compile "com.sun.xml.bind:jaxb-core:2.3.0.1"
    compile "com.sun.xml.bind:jaxb-impl:2.3.2"
    compile "javax.activation:javax.activation-api:1.2.0"
    compile "javax.mail:javax.mail-api:1.6.2"
    compile "io.swagger:swagger-annotations:$swaggerVersion"
    compile "com.squareup.okhttp:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp:logging-interceptor:$okhttpVersion"
    compile "com.google.code.gson:gson:2.8.5"
    compile "io.gsonfire:gson-fire:1.8.0"
    compile "org.threeten:threetenbp:1.3.8"
    compile "org.controlsfx:controlsfx:$controlsfxVersion"
    compile "org.drjekyll:fontchooser:2.4"
}

/* 
 * Remove the xml-apis dependencies to avoid a compilation error in Eclipse. 
 * The org.w3c.dom package is present in the java.xml module as well as in these dependencies. 
 * That is illegal (https://bugs.eclipse.org/bugs/show_bug.cgi?id=536928).
 */
configurations.all {
  exclude group: "xml-apis", module: "xml-apis"
  exclude group: "xml-apis", module: "xml-apis-ext"
  exclude group: "xom", module: "xom"
  exclude group: "ch.qos.logback", module: "logback-core"
  exclude group: "ch.qos.logback", module: "logback-classic"
}

javafx {
    version = "12"
    modules = ["javafx.controls", 
               "javafx.swing", 
               "javafx.fxml",
               "javafx.web",
               "javafx.graphics"
              ]
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

/*
 * This is important in order to copy all .java and .fxml files into the final MZmine jar. 
 * The .java files are only for reference, for people who want to check the source codes.
 * But the .fxml files are required for the GUI.
 */
jar {
  from sourceSets.main.allSource
}

application {
    mainClassName = "net.sf.mzmine.main.MZmineCore"
    applicationName = "MZmine"
    executableDir = ""
    applicationDefaultJvmArgs = ["-showversion", 
                                 "-XX:MaxHeapFreeRatio=100", 
                                 "-XX:InitialRAMPercentage=30", 
                                 "-XX:MinRAMPercentage=80",
                                 "-XX:MaxRAMPercentage=80",
                                 "-Djava.library.path=lib",
                                 "-Dapple.awt.application.name=MZmine ${version}", 
                                 "-Dapple.laf.useScreenMenuBar=true"
                                ]
}

startScripts {
    applicationName = "startMZmine-" + osSuffix
    classpath = files("lib/*")
    doLast {
        if (OperatingSystem.current().isWindows()) {
            delete unixScript
            // Temporary fix for https://github.com/gradle/gradle/issues/10387
            windowsScript.text = windowsScript.text.replace('set JAVA_HOME=\"%~dp0.."', 'set JAVA_HOME="%~dp0"')
        } else {
            delete windowsScript
        }
    }
}

runtime {
    options = ["--compress=2", "--vm=server", "--no-header-files", "--no-man-pages"]
    modules = ["java.desktop",
               "java.logging",
               "java.xml",
               "java.sql",
               "java.datatransfer",
               "java.security.sasl",
               "java.scripting",
               "jdk.unsupported"
              ]
    imageDir = file("${buildDir}/${applicationName}-${version}-${osSuffix}")
    imageZip = file("${buildDir}/${applicationName}-${version}-${osSuffix}.zip")
}

tasks.runtime.doLast {
    copy {
        from("src/main/icons")
        into("${buildDir}/${applicationName}-${version}-${osSuffix}/icons")
    }
    copy {
        from("src/main/log")
        into("${buildDir}/${applicationName}-${version}-${osSuffix}/log")
    }
    copy {
        from("src/main/conf/logging.properties")
        into("${buildDir}/${applicationName}-${version}-${osSuffix}/conf")
    } 
}

// Add MZmine version number to version.properties
processResources {
    filesMatching("mzmineversion.properties") { 
        expand(project.properties) 
    }
}
